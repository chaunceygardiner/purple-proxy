#!/usr/bin/perl

# Copyright (c) 2020 John A Kline
# See the file LICENSE for your full rights.


use strict;

my %counts;
my %errors;

# keys for individual counts
my $STARTUPS = 'Startups';
my $LONG_SENSOR_READS = 'Long Sensor Reads';
my $ARCHIVE_RECORDS_ADDED = 'Archive Records Added';
my $SKIPPED_SENSOR_READINGS = 'Skipped Sensor Readings';
my $ARCHIVE_INSERT_ERRORS = 'Archive Insert Errors';
my $CURRENT_INSERT_ERRORS = 'Current Insert Errors';

# any lines that do not match the patterns we define
my @unmatched = ();

# keep details
my @skipped_sensor_readings = ();
my @current_insert_errors = ();
my @archive_insert_errors = ();

my %itemized = (
    'Skipped Sensor Readings', \@skipped_sensor_readings,
    'Archive Insert Errors', \@archive_insert_errors,
    'Current Insert Errors', \@current_insert_errors,
    );

my %summaries = (
    'counts', \%counts,
    'errors', \%errors,
    );

while(defined($_ = <STDIN>)) {
    chomp;
    if (/Version *:/) {
        $counts{$STARTUPS} += 1;
    } elsif (/Event took longer than expected/) {
        $counts{$LONG_SENSOR_READS} += 1;
    } elsif (/Added record/) {
        $counts{$ARCHIVE_RECORDS_ADDED} += 1;
    } elsif (/Could not save archive reading to database/) {
        push @archive_insert_errors, $_;
        $counts{$ARCHIVE_INSERT_ERRORS} += 1;
    } elsif (/Could not save current reading to database/) {
        push @current_insert_errors, $_;
        $counts{$CURRENT_INSERT_ERRORS} += 1;
    } elsif (/Skipping reading because of:/) {
        push @skipped_sensor_readings, $_;
        $counts{$SKIPPED_SENSOR_READINGS} += 1;
    } elsif (/host:port *:/ ||
             /conf_file *:/ ||
             /server_port *:/ ||
             /db_file *:/ ||
             /timeout_secs *:/ ||
             /pollfreq_secs *:/ ||
             /pollfreq_offset *:/ ||
             /arcint_secs *:/ ||
             /outfile *:/ ||
             /arcfile *:/ ||
             /service_name *:/ ||
             /pidfile *:/ ||
             /log_to_stdout *:/ ||
             /Starting .* daemon:/ ||
             /debug *:/) {
        # ignore
    } else {
        push @unmatched, $_;
    }
}

foreach my $slabel (sort keys %summaries) {
    my $s = $summaries{$slabel};
    if(scalar(keys %$s)) {
        print "$slabel:\n";
        foreach my $k (sort keys %$s) {
            next if $s->{$k} == 0;
            printf("  %-45s %6d\n", $k, $s->{$k});
        }
        print "\n";
    }
}

foreach my $k (sort keys %itemized) {
    report($k, $itemized{$k}) if scalar @{$itemized{$k}} > 0;
}

report("unmatched lines", \@unmatched) if $#unmatched >= 0;

exit 0;

sub report {
    my($label, $aref, $href) = @_;
    print "\n$label:\n";
    foreach my $x (@$aref) {
        my $str = $x;
        if ($href && $href->{$x} > 1) {
            $str .= " ($href->{$x} times)";
        }
        print "  $str\n";
    }
}
